/*
 * Copyright (C) 2015-2018 akha, a.k.a. Alexander Kharitonov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        flatDir dirs: '../yakhont-weaver/build/libs'                        // Yakhont weaver
    }
    dependencies {
        classpath "akha.yakhont.weaver:yakhont-weaver:0.9.19"               // Yakhont weaver
        classpath "org.javassist:javassist:${rootProject.ext.projectLibVerJavassist}"
    }
}

apply plugin: 'com.android.application'

ext.libVerDemoSimpleDefault     = rootProject.ext.projectLibVerDefault

ext.libVerDemoSimpleSupport     = rootProject.ext.projectLibVerSupport
ext.libVerDemoSimpleLifecycle   = rootProject.ext.projectLibVerLifecycle
ext.libVerDemoSimpleLocation    = rootProject.ext.projectLibVerLocation

ext.libVerDemoSimpleRetrofit2   = rootProject.ext.projectLibVerRetrofit2

ext.libVerDemoSimpleOkHttp3     = rootProject.ext.projectLibVerOkHttp3
ext.libVerDemoSimpleGson        = rootProject.ext.projectLibVerGson

ext.libVerDemoSimpleRxJava      = rootProject.ext.projectLibVerRxJava
ext.libVerDemoSimpleRxJava2     = rootProject.ext.projectLibVerRxJava2

ext.libVerDemoSimpleDagger      = rootProject.ext.projectLibVerDagger

def dependenciesStrategy = { DependencyResolveDetails details ->
    if (details.requested.group == 'com.android.support') {
        details.useVersion          libVerDemoSimpleSupport
//      details.because 'To enforce releasable units for Support Libraries'
    }
    if (details.requested.group.startsWith('android.arch.')) {
        details.useVersion          libVerDemoSimpleLifecycle
//      details.because 'To enforce releasable units for Support Libraries (arch)'
    }
    if (details.requested.group == 'com.google.android.gms') {
        details.useVersion          libVerDemoSimpleLocation
//      details.because 'To enforce releasable units for Google Play Services'
    }
    if (details.requested.group == 'com.squareup.retrofit2') {
        details.useVersion          libVerDemoSimpleRetrofit2
//      details.because 'To enforce releasable units for Retrofit 2'
    }
    if (details.requested.group == 'com.squareup.okhttp3') {
        details.useVersion          libVerDemoSimpleOkHttp3
//      details.because 'To enforce releasable units for okhttp3'
    }
    if (details.requested.group == 'com.google.dagger') {
        details.useVersion          libVerDemoSimpleDagger
//      details.because 'To enforce releasable units for Dagger 2'
    }
}

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion           27
    buildToolsVersion          '27.0.3'

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
        applicationId           'akha.yakhont.demosimple'
        minSdkVersion           14
        targetSdkVersion        27
        versionCode            10101
        versionName           '1.1'

        resConfigs            'en', 'ru'
    }

    compileOptions {
        sourceCompatibility     JavaVersion.VERSION_1_8
        targetCompatibility     JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError            false
    }

    dataBinding {
        enabled                 true
    }

    buildTypes {
        release {
            minifyEnabled       rootProject.ext.projectDebug != '1'

            rootProject.ext.projectProGuardFiles.each {
                //noinspection GroovyAssignabilityCheck
                proguardFile            '../proguard/libs/' + it
            }
            proguardFile                '../proguard/libs/proguard-gson.pro'

            proguardFile                './proguard/proguard-project-app.pro'
            proguardFile                '../proguard/proguard-project.pro'
            //noinspection GroovyAssignabilityCheck
            proguardFile                getDefaultProguardFile('proguard-android.txt')
        }
    }

    // workaround for the local Yakhont aar
    repositories {
        flatDir dirs: '../yakhont/build/outputs/aar'
    }

    applicationVariants.all { variant ->
        String prefix = variant.buildType.name
        configurations.getByName(prefix + 'UnitTestCompileClasspath')
                                                     .resolutionStrategy.eachDependency dependenciesStrategy
        configurations.getByName(prefix + 'UnitTestRuntimeClasspath')
                                                     .resolutionStrategy.eachDependency dependenciesStrategy
        variant.getCompileConfiguration()            .resolutionStrategy.eachDependency dependenciesStrategy
        variant.runtimeConfiguration                 .resolutionStrategy.eachDependency dependenciesStrategy
        variant.getAnnotationProcessorConfiguration().resolutionStrategy.eachDependency dependenciesStrategy
    }
    testVariants      .all { variant ->
        variant.getCompileConfiguration()            .resolutionStrategy.eachDependency dependenciesStrategy
        variant.getRuntimeConfiguration()            .resolutionStrategy.eachDependency dependenciesStrategy
    }
}

dependencies {
    // v4 Support Libraries
    // fragment has dependencies on compat, core-utils, core-ui, and media-compat
    implementation      "com.android.support:support-fragment:${libVerDemoSimpleDefault}"
    implementation      "com.android.support:support-compat:${libVerDemoSimpleDefault}"
    implementation      "com.android.support:support-core-utils:${libVerDemoSimpleDefault}"
    implementation      "com.android.support:support-core-ui:${libVerDemoSimpleDefault}"
    implementation      "com.android.support:support-media-compat:${libVerDemoSimpleDefault}"
    // v7 Support Libraries
    // appcompat (includes support for material design) depends on the v4 Support Library
    implementation      "com.android.support:appcompat-v7:${libVerDemoSimpleDefault}"
    implementation      "com.android.support:recyclerview-v7:${libVerDemoSimpleDefault}"
    // v13 Support Library (support for the Fragment with the FragmentCompat)
    implementation      "com.android.support:support-v13:${libVerDemoSimpleDefault}"

    implementation      "com.android.support:support-annotations:${libVerDemoSimpleDefault}"
//    implementation      "com.android.support:design:${libVerDemoSimpleDefault}"
    // comment out if you don't need Location API
    // (and uncomment lines in proguard-project-app.pro)
    implementation      "com.google.android.gms:play-services-location:${libVerDemoSimpleDefault}"
    implementation      "android.arch.lifecycle:common-java8:${libVerDemoSimpleDefault}"
    implementation      "android.arch.lifecycle:extensions:${libVerDemoSimpleDefault}"

    implementation      "com.google.dagger:dagger:${libVerDemoSimpleDefault}"           // Dagger 2
    annotationProcessor "com.google.dagger:dagger-compiler:${libVerDemoSimpleDefault}"

    implementation      "com.google.code.gson:gson:${libVerDemoSimpleGson}"             // Gson

    implementation     ("com.squareup.retrofit2:retrofit:${libVerDemoSimpleDefault}") { // Retrofit
        exclude group:  "com.squareup.okhttp3", module: "okhttp"
    }
    implementation     ("com.squareup.retrofit2:converter-gson:${libVerDemoSimpleDefault}") {
        exclude group:  "com.google.code.gson", module: "gson"
    }
    implementation      "com.squareup.okhttp3:okhttp:${libVerDemoSimpleDefault}"
    implementation      "com.squareup.okhttp3:logging-interceptor:${libVerDemoSimpleDefault}"

    implementation      "io.reactivex.rxjava2:rxjava:${libVerDemoSimpleRxJava2}"        // Rx
    implementation      "io.reactivex:rxjava:${libVerDemoSimpleRxJava}"

    implementation      name:'yakhont', ext:'aar'                                       // Yakhont
}

// for demo simple let's build both release and debug - just to be sure the weaver is working fine

String[] weaverConfigFiles = null   // use default one; or something like "new String[] {projectDir.absolutePath + '/weaver.config'}"
String pkg = android.defaultConfig.applicationId
boolean weaverDebug = false, weaverAddConfig = true

android.applicationVariants.all { variant ->
    AbstractCompile javaCompile = variant.javaCompiler
    javaCompile.doLast {
        //noinspection UnnecessaryQualifiedReference
        new akha.yakhont.weaver.Weaver().run(variant.buildType.name == 'debug', weaverDebug, pkg,
                javaCompile.destinationDir.toString(), javaCompile.classpath.asPath,
                android.bootClasspath.join(File.pathSeparator), weaverConfigFiles, weaverAddConfig)
    }
}

/* if (by some reason) you're about to use the deprecated Transform API, you can try the following:
//noinspection GroovyAssignabilityCheck, UnnecessaryQualifiedReference
android.registerTransform(new akha.yakhont.weaver.WeaverTransform(weaverDebug, android.defaultConfig.applicationId,
        android.bootClasspath.join(File.pathSeparator), weaverConfigFiles, weaverAddConfig))
*/
