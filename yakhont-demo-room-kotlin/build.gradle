/*
 * Copyright (C) 2015-2020 akha, a.k.a. Alexander Kharitonov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import akha.yakhont.weaver.Weaver

buildscript {
    repositories {
        jcenter()
        flatDir dirs: '../yakhont-weaver/build/libs'                        // Yakhont weaver
    }
    dependencies {
        classpath "akha.yakhont.weaver:yakhont-weaver:$projectVerName"
        classpath "org.javassist:javassist:$projectLibVerJavassist"         // Yakhont weaver
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def dependenciesStrategy = { DependencyResolveDetails details ->
    // comment out if you don't need Location API
    if     (details.requested.group == 'com.google.android.gms') {
            details.useVersion      projectLibVerGms
        if (details.requested.name  == 'play-services-location')
            details.useVersion      projectLibVerLocation
        if (details.requested.name  == 'play-services-places-placereport')
            details.useVersion      projectLibVerPlacesPlacereport
        details.because 'To enforce releasable units for Google Play Services'
    }
    if (details.requested.group == 'com.squareup.retrofit2') {
        details.useVersion          projectLibVerRetrofit2
        details.because 'To enforce releasable units for Retrofit 2'
    }
    if (details.requested.group == 'com.squareup.okhttp3') {
        details.useVersion          projectLibVerOkHttp3
        details.because 'To enforce releasable units for OkHttp 3'
    }
    if (details.requested.group == 'com.google.dagger') {
        details.useVersion          projectLibVerDagger
        details.because 'To enforce releasable units for Dagger 2'
    }
}

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion           projectVerSdk
    buildToolsVersion           projectVerBuildTools

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
        applicationId          'akha.yakhont.demoroomkotlin'
        multiDexEnabled         true                        // for debug builds
        minSdkVersion           projectVerSdkMin
        targetSdkVersion        projectVerSdk
        versionCode             projectVerCodeDemo
        versionName             projectVerNameDemo
        resConfigs              projectResConfigs
    }

    compileOptions {
        targetCompatibility     JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError            false
    }

    buildFeatures {
        dataBinding             true
    }

    buildTypes {
        release {
            minifyEnabled       projectConfig != projectNoObfuscate

            projectProGuardFiles.each {
                //noinspection GroovyAssignabilityCheck
                proguardFile            '../proguard/libs/' + it
            }
            proguardFile                '../proguard/libs/proguard-gson.pro'

            proguardFile                './proguard/proguard-project-app.pro'
            proguardFile                '../proguard/proguard-project.pro'
            //noinspection GroovyAssignabilityCheck
            proguardFile                getDefaultProguardFile('proguard-android.txt')
        }
    }

    repositories {
        flatDir dirs: '../yakhont/build/outputs/aar'
    }

    applicationVariants.all { variant ->
        String prefix = variant.buildType.name
        configurations.getByName(prefix + 'UnitTestCompileClasspath')
                                                     .resolutionStrategy.eachDependency dependenciesStrategy
        configurations.getByName(prefix + 'UnitTestRuntimeClasspath')
                                                     .resolutionStrategy.eachDependency dependenciesStrategy
        variant.getCompileConfiguration()            .resolutionStrategy.eachDependency dependenciesStrategy
        variant.runtimeConfiguration                 .resolutionStrategy.eachDependency dependenciesStrategy
        variant.getAnnotationProcessorConfiguration().resolutionStrategy.eachDependency dependenciesStrategy
    }
    testVariants      .all { variant ->
        variant.getCompileConfiguration()            .resolutionStrategy.eachDependency dependenciesStrategy
        variant.getRuntimeConfiguration()            .resolutionStrategy.eachDependency dependenciesStrategy
    }
}

dependencies {
    implementation      "androidx.multidex:multidex:$projectLibVerMultidex"     // for debug builds
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$projectLibVerLeakCanary"

    implementation      "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation      "androidx.core:core-ktx:$projectLibVerKotlinCoreCore"
    kapt                "com.android.databinding:compiler:$projectLibVerKotlinDatabindingCompiler"

    implementation      "com.google.dagger:dagger:$projectLibVerDefault"
    kapt                "com.google.dagger:dagger-compiler:$projectLibVerDagger"

    implementation      "androidx.appcompat:appcompat:$projectLibVerAppcompatAppcompat"
    implementation      "androidx.recyclerview:recyclerview:$projectLibVerRecyclerViewRecyclerView"

    implementation      "androidx.lifecycle:lifecycle-common-java8:$projectLibVerLifecycleLifecycleCommonJava8"
    implementation      "androidx.lifecycle:lifecycle-extensions:$projectLibVerLifecycleLifecycleExtensions"
    implementation      "androidx.lifecycle:lifecycle-viewmodel-ktx:$projectLibVerKotlinLifecycleLifecycleViewmodel"
    implementation      "androidx.lifecycle:lifecycle-reactivestreams-ktx:$projectLibVerKotlinLifecycleLifecycleReactiveStreams"

    // comment out if you don't need Location API
    // (and uncomment lines in proguard-project-app.pro)
    implementation      "com.google.android.material:material:$projectLibVerNotxMaterialMaterial"
    implementation      "com.google.android.gms:play-services-location:$projectLibVerDefault"

    implementation      "com.google.code.gson:gson:$projectLibVerGson"

    //noinspection GradleDependency
    implementation     ("com.squareup.retrofit2:retrofit:$projectLibVerDefault") {
        exclude group:  "com.squareup.okhttp3", module: "okhttp"
    }
    //noinspection GradleDependency
    implementation     ("com.squareup.retrofit2:converter-gson:$projectLibVerDefault") {
        exclude group:  "com.google.code.gson", module: "gson"
    }

    //noinspection GradleDependency
    implementation      "com.squareup.okhttp3:okhttp:$projectLibVerDefault"
    implementation      "com.squareup.okhttp3:logging-interceptor:$projectLibVerDefault"

    implementation      "io.reactivex.rxjava3:rxjava:$projectLibVerRxJava3"

    implementation      "androidx.room:room-runtime:$projectLibVerRoom"
    kapt                "androidx.room:room-compiler:$projectLibVerRoom"
    implementation      "androidx.room:room-ktx:$projectLibVerRoom"
    implementation      "androidx.room:room-rxjava2:$projectLibVerRoom"

    implementation      "com.github.akhasoft:yakhont:$projectVerName@aar"
}

// let's build release (or debug) only - just to reduce building time
android.variantFilter { variant ->
    if (variant.buildType.name == (projectConfig == projectDebug ? 'release': 'debug')) {
        variant.setIgnore(true)
    }
}

// null means default config (or provide something like "projectDir.absolutePath + '/your.config'")
//   or: String[] weaverConfigFiles = new String[] {projectDir.absolutePath + '/your_1.config' /*, ...*/ }
String weaverConfigFiles = null, pkg = android.defaultConfig.applicationId
boolean weaverDebug = false, weaverAddConfig = true

android.applicationVariants.all { variant ->
    JavaCompile javaCompile = variant.javaCompileProvider.get()

    javaCompile.doLast {
        String classesDirs = javaCompile.destinationDir.toString() + File.pathSeparator +
                buildDir.toString() + '/tmp/kotlin-classes/'.replace('/', File.separator) +
                (projectConfig == projectDebug ? 'debug': 'release')

        Weaver.run(variant.buildType.name == 'debug', weaverDebug, pkg, classesDirs,
                // classesDirs is needed for weaving using application's code
                javaCompile.classpath.asPath + File.pathSeparator + classesDirs,
                android.bootClasspath.join(File.pathSeparator), weaverAddConfig, weaverConfigFiles)
    }
}
