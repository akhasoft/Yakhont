/*
 * Copyright (C) 2015-2018 akha, a.k.a. Alexander Kharitonov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        flatDir dirs: '../yakhont-weaver/build/libs'                        // Yakhont weaver
    }
    dependencies {
        classpath "akha.yakhont.weaver:yakhont-weaver:0.9.19"               // Yakhont weaver
        classpath "org.javassist:javassist:${rootProject.ext.projectLibVerJavassist}"
    }
}

apply plugin: 'com.android.application'

ext.libVerDemoDefault           = rootProject.ext.projectLibVerDefault

ext.libVerDemoSupport           = rootProject.ext.projectLibVerSupport
ext.libVerDemoLifecycle         = rootProject.ext.projectLibVerLifecycle

ext.libVerDemoGms               = rootProject.ext.projectLibVerGms
ext.libVerDemoLocation          = rootProject.ext.projectLibVerLocation

ext.libVerDemoRetrofit          = rootProject.ext.projectLibVerRetrofit
ext.libVerDemoRetrofit2         = rootProject.ext.projectLibVerRetrofit2

ext.libVerDemoOkHttp3           = rootProject.ext.projectLibVerOkHttp3
ext.libVerDemoGson              = rootProject.ext.projectLibVerGson

ext.libVerDemoRxJava            = rootProject.ext.projectLibVerRxJava
ext.libVerDemoRxJava2           = rootProject.ext.projectLibVerRxJava2

ext.libVerDemoDagger            = rootProject.ext.projectLibVerDagger

def dependenciesStrategy = { DependencyResolveDetails details ->
    if (details.requested.group == 'com.android.support') {
        details.useVersion          libVerDemoSupport
        details.because 'To enforce releasable units for Support Libraries'
    }
    if (details.requested.group.startsWith('android.arch.')) {
        details.useVersion          libVerDemoLifecycle
        details.because 'To enforce releasable units for Support Libraries (arch)'
    }
    if (details.requested.group == 'com.google.android.gms') {
        if (details.requested.name == 'play-services-location' || 
            details.requested.name == 'play-services-places-placereport')
            details.useVersion      libVerDemoLocation
        else
            details.useVersion      libVerDemoGms
        details.because 'To enforce releasable units for Google Play Services'
    }
    if (details.requested.group == 'com.squareup.retrofit2') {
        details.useVersion          libVerDemoRetrofit2
        details.because 'To enforce releasable units for Retrofit 2'
    }
    if (details.requested.group == 'com.squareup.okhttp3') {
        details.useVersion          libVerDemoOkHttp3
        details.because 'To enforce releasable units for okhttp3'
    }
    if (details.requested.group == 'com.google.dagger') {
        details.useVersion          libVerDemoDagger
        details.because 'To enforce releasable units for Dagger 2'
    }
}

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion           28
    buildToolsVersion          '28.0.3'

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
        applicationId           'akha.yakhont.demo'
        minSdkVersion           14
        targetSdkVersion        28
        versionCode            10101
        versionName           '1.1'

        resConfigs            'en', 'ru'
    }

    compileOptions {
        sourceCompatibility     JavaVersion.VERSION_1_8
        targetCompatibility     JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError            false
    }

    dataBinding {
        enabled                 true
    }

    buildTypes {
        release {
            minifyEnabled       rootProject.ext.projectDebug != '1'

            // normally it added from aar automatically - but this demo is not dependent on yakhont.aar
            // proguardFile                '../yakhont/proguard/proguard-consumer.pro'

            rootProject.ext.projectProGuardFiles.each {
                //noinspection GroovyAssignabilityCheck
                proguardFile            '../proguard/libs/' + it
            }
            proguardFile                '../proguard/libs/proguard-gson.pro'

            proguardFile                './proguard/libs/proguard-support-design.pro'
            proguardFile                './proguard/proguard-project-app.pro'
            proguardFile                '../proguard/proguard-project.pro'
            //noinspection GroovyAssignabilityCheck
            proguardFile                getDefaultProguardFile('proguard-android.txt')
        }
    }

    // workaround for the local Yakhont aar
    repositories {
        flatDir dirs: '../yakhont/build/outputs/aar'
    }

    // fixed DuplicateFileException when both RxJava 1.x and RxJava 2.x are in use
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    applicationVariants.all { variant ->
        String prefix = variant.buildType.name
        configurations.getByName(prefix + 'UnitTestCompileClasspath')
                                                     .resolutionStrategy.eachDependency dependenciesStrategy
        configurations.getByName(prefix + 'UnitTestRuntimeClasspath')
                                                     .resolutionStrategy.eachDependency dependenciesStrategy
        variant.getCompileConfiguration()            .resolutionStrategy.eachDependency dependenciesStrategy
        variant.runtimeConfiguration                 .resolutionStrategy.eachDependency dependenciesStrategy
        variant.getAnnotationProcessorConfiguration().resolutionStrategy.eachDependency dependenciesStrategy
    }
    testVariants      .all { variant ->
        variant.getCompileConfiguration()            .resolutionStrategy.eachDependency dependenciesStrategy
        variant.getRuntimeConfiguration()            .resolutionStrategy.eachDependency dependenciesStrategy
    }
}

dependencies {
    // v4 Support Libraries
    // fragment has dependencies on compat, core-utils, core-ui, and media-compat
    implementation      "com.android.support:support-fragment:${libVerDemoDefault}"
    implementation      "com.android.support:support-compat:${libVerDemoDefault}"
    implementation      "com.android.support:support-core-utils:${libVerDemoDefault}"
    implementation      "com.android.support:support-core-ui:${libVerDemoDefault}"
    implementation      "com.android.support:support-media-compat:${libVerDemoDefault}"
    // v7 Support Libraries
    // appcompat (includes support for material design) depends on the v4 Support Library
    implementation      "com.android.support:appcompat-v7:${libVerDemoDefault}"
//    implementation      "com.android.support:recyclerview-v7:${libVerDemoDefault}"
    // v13 Support Library (support for the Fragment with the FragmentCompat)
    implementation      "com.android.support:support-v13:${libVerDemoDefault}"

    implementation      "com.android.support:support-annotations:${libVerDemoDefault}"
    implementation      "com.android.support:design:${libVerDemoDefault}"
    implementation      "com.google.android.gms:play-services-location:${libVerDemoDefault}"
    implementation      "android.arch.lifecycle:common-java8:${libVerDemoDefault}"
    implementation      "android.arch.lifecycle:extensions:${libVerDemoDefault}"

    implementation      "com.google.dagger:dagger:${libVerDemoDefault}"             // Dagger 2
    annotationProcessor "com.google.dagger:dagger-compiler:${libVerDemoDefault}"

    implementation      "com.google.code.gson:gson:${libVerDemoGson}"               // Gson

    implementation     ("com.squareup.retrofit2:retrofit:${libVerDemoDefault}") {   // Retrofit
        exclude group:  "com.squareup.okhttp3", module: "okhttp"
    }
    implementation     ("com.squareup.retrofit2:converter-gson:${libVerDemoDefault}") {
        exclude group:  "com.google.code.gson", module: "gson"
    }
    implementation      "com.squareup.okhttp3:okhttp:${libVerDemoDefault}"
    implementation      "com.squareup.okhttp3:logging-interceptor:${libVerDemoDefault}"

    implementation      "com.squareup.retrofit2:adapter-rxjava:${libVerDemoDefault}"
    implementation      "com.squareup.retrofit2:adapter-rxjava2:${libVerDemoDefault}"

    implementation     ("com.squareup.retrofit:retrofit:${libVerDemoRetrofit}") {
        exclude group:  "com.google.code.gson", module: "gson"
    }

    implementation      "io.reactivex.rxjava2:rxjava:${libVerDemoRxJava2}"          // Rx
    implementation      "io.reactivex:rxjava:${libVerDemoRxJava}"

    implementation      name:'yakhont', ext:'aar'                           // tmp stub
//    implementation      name:'yakhont-support', ext:'aar'                           // Yakhont
// 'cause support sources doesn't exist and generated 'on the fly'
//    implementation project(':yakhont')
}

android.variantFilter { variant ->
    // let's build release (or debug) only - just to reduce building time
    if (variant.buildType.name == (rootProject.ext.projectDebug == '1' ? 'release': 'debug')) {
        variant.setIgnore(true)
    }
}

String[] weaverConfigFiles = null   // use default one; or something like "new String[] {projectDir.absolutePath + '/weaver.config'}"
String pkg = android.defaultConfig.applicationId
boolean weaverDebug = false, weaverAddConfig = true

android.applicationVariants.all { variant ->
    AbstractCompile javaCompile = variant.javaCompiler
    javaCompile.doLast {
        //noinspection UnnecessaryQualifiedReference
        new akha.yakhont.weaver.Weaver().run(variant.buildType.name == 'debug', weaverDebug, pkg,
                javaCompile.destinationDir.toString(), javaCompile.classpath.asPath,
                android.bootClasspath.join(File.pathSeparator), weaverConfigFiles, weaverAddConfig)
    }
}

/* if (by some reason) you're about to use the deprecated Transform API, you can try the following:
//noinspection GroovyAssignabilityCheck, UnnecessaryQualifiedReference
android.registerTransform(new akha.yakhont.weaver.WeaverTransform(weaverDebug, android.defaultConfig.applicationId,
        android.bootClasspath.join(File.pathSeparator), weaverConfigFiles, weaverAddConfig))
*/
