/*
 * Copyright (C) 2015-2018 akha, a.k.a. Alexander Kharitonov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'

apply from:   'preprocessor.gradle'

String verName                  = '0.9.19'
int    verCode                  =    901901

ext {
    publishedName               = 'Yakhont'
    publishedDescription        = 'The high-level Android components for data loading, location, lifecycle callbacks and many more'

    publishedGroupId            = rootProject.ext.projectGroupId
    publishedArtifactId         = 'yakhont'
    publishedPackaging          = 'aar'
    publishedVersion            = verName

    projectDefaultFlavor        = 'core'
    projectJavaDocLinks         = ['http://google.github.io/dagger/api/latest/'             ,
                                   'http://google.github.io/gson/apidocs/'                  ,
                                   'http://reactivex.io/RxJava/1.x/javadoc/'                ,
                                   'http://reactivex.io/RxJava/2.x/javadoc/'                ,
                                   'http://square.github.io/retrofit/1.x/retrofit/'         ,
                                   'http://square.github.io/retrofit/2.x/retrofit/']
}

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion           27
    buildToolsVersion          '27.0.3'

    defaultConfig {
        minSdkVersion           14
        targetSdkVersion        27
        versionCode        verCode
        versionName        verName
    }

    compileOptions {
        sourceCompatibility     JavaVersion.VERSION_1_8
        targetCompatibility     JavaVersion.VERSION_1_8
    }

    flavorDimensions           'common'

    productFlavors {
        core {
            dimension          'common'
        }
        support {   // same as core but based on support library (android.support.v4.app.Fragment etc.)
            dimension          'common'
        }
        full {      // core + support + debug classes for Activities and Fragments
            dimension          'common'
        }
    }

    defaultPublishConfig       'support' + (rootProject.ext.projectDebug == '1' ? 'Debug': 'Release')

    lintOptions {
        abortOnError            false
    }

    buildTypes {
        release {
            minifyEnabled       true

            rootProject.ext.projectProGuardFiles.each {
                //noinspection GroovyAssignabilityCheck
                proguardFile            '../proguard/libs/' + it
            }
            proguardFile                './proguard/proguard-consumer.pro'
            proguardFile                './proguard/proguard-project-lib.pro'
            proguardFile                '../proguard/proguard-project.pro'
            //noinspection GroovyAssignabilityCheck
            proguardFile                getDefaultProguardFile('proguard-android.txt')

            consumerProguardFiles       './proguard/proguard-consumer.pro'
        }
    }
}

ext.libVerSupport               = rootProject.ext.projectLibVerSupport
ext.libVerLifecycle             = rootProject.ext.projectLibVerLifecycle
ext.libVerLocation              = rootProject.ext.projectLibVerLocation

ext.libVerRetrofit              = rootProject.ext.projectLibVerRetrofit
ext.libVerRetrofit2             = rootProject.ext.projectLibVerRetrofit2

ext.libVerOkHttp3               = rootProject.ext.projectLibVerOkHttp3
ext.libVerOkHttp                = '2.7.5'

ext.libVerRxJava                = rootProject.ext.projectLibVerRxJava
ext.libVerRxJava2               = rootProject.ext.projectLibVerRxJava2

ext.libVerDagger                = rootProject.ext.projectLibVerDagger

dependencies {
    // v4 Support Libraries
    // fragment has dependencies on compat, core-utils, core-ui, and media-compat
    api                 "com.android.support:support-fragment:${libVerSupport}"
    api                 "com.android.support:support-compat:${libVerSupport}"
    api                 "com.android.support:support-core-utils:${libVerSupport}"
    api                 "com.android.support:support-core-ui:${libVerSupport}"
    api                 "com.android.support:support-media-compat:${libVerSupport}"
    // v7 Support Libraries
    // appcompat (includes support for material design) depends on the v4 Support Library
    api                 "com.android.support:appcompat-v7:${libVerSupport}"
    api                 "com.android.support:recyclerview-v7:${libVerSupport}"
    // v13 Support Library (support for the Fragment with the FragmentCompat)
    api                 "com.android.support:support-v13:${libVerSupport}"

    api                 "com.android.support:support-annotations:${libVerSupport}"
    api                 "com.android.support:design:${libVerSupport}"
    api                 "com.google.android.gms:play-services-location:${libVerLocation}"
    api                 "android.arch.lifecycle:common-java8:${libVerLifecycle}"
    api                 "android.arch.lifecycle:extensions:${libVerLifecycle}"

    // for debug package only
    fullApi             "com.android.support:leanback-v17:${libVerSupport}"
    fullApi             "com.android.support:mediarouter-v7:${libVerSupport}"
    fullApi             "com.android.support:preference-v14:${libVerSupport}"
    fullApi             "com.android.support:preference-leanback-v17:${libVerSupport}"

    api                 "com.google.dagger:dagger:${libVerDagger}"           // Dagger2
    annotationProcessor "com.google.dagger:dagger-compiler:${libVerDagger}"

    api                 "com.google.code.gson:gson:2.8.2"                    // Gson

    api                 "com.squareup.picasso:picasso:2.5.2"                 // Picasso

    api                 "com.squareup.retrofit2:retrofit:${libVerRetrofit2}" // Retrofit
    api                 "com.squareup.retrofit2:converter-gson:${libVerRetrofit2}"
    api                 "com.squareup.okhttp3:okhttp:${libVerOkHttp3}"
    api                 "com.squareup.okhttp3:logging-interceptor:${libVerOkHttp3}"

    api                 "com.squareup.retrofit2:adapter-rxjava:${libVerRetrofit2}"
    api                 "com.squareup.retrofit2:adapter-rxjava2:${libVerRetrofit2}"

    api                 "com.squareup.retrofit:retrofit:${libVerRetrofit}"
    api                 "com.squareup.okhttp:okhttp:${libVerOkHttp}"
    api                 "com.squareup.okhttp:okhttp-urlconnection:${libVerOkHttp}"

    api                 "io.reactivex.rxjava2:rxjava:${libVerRxJava2}"       // Rx
    api                 "io.reactivex:rxjava:${libVerRxJava}"
}

apply from: '../publish.gradle'
